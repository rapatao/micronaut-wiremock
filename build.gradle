plugins {
    id "signing"
    id 'maven-publish'
    id "com.github.ben-manes.versions" version "${benManesVersionsVersion}"
    id "io.github.gradle-nexus.publish-plugin" version "${publishPluginVersion}"
    id "io.gitlab.arturbosch.detekt" version "${detektPluginVersion}"
    id "io.micronaut.minimal.library" version "${micronautPluginVersion}"
    id "org.jetbrains.dokka" version "${dokkaVersion}"
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
    id "org.jetbrains.kotlin.kapt" version "${kotlinVersion}"
    id "org.jetbrains.kotlin.plugin.allopen" version "${kotlinVersion}"
    id "org.jetbrains.kotlinx.kover" version "${koverPluginVersion}"
}

group = 'com.rapatao.micronaut'

repositories {
    mavenCentral()
}

micronaut {
    version "${micronautVersion}"
    testRuntime "junit5"
    processing {
        incremental true
        annotations "com.rapatao.micronaut.wiremock.*"
    }
}

dependencies {
    api("org.wiremock:wiremock:${wiremockVersion}")

    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
    implementation("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")

    compileOnly("org.junit.jupiter:junit-jupiter-api")

    testImplementation("io.micronaut.serde:micronaut-serde-jackson")
    testImplementation("io.micronaut:micronaut-http-client")
}

java {
    withSourcesJar()
    withJavadocJar()
}

compileKotlin {
    kotlinOptions.jvmTarget = "${javaVersion}"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "${javaVersion}"
}

test {
    useJUnitPlatform()
}

kover {
    reports {
        verify {
            rule {
                minBound(90)
            }
        }
    }
}

detekt {
    buildUponDefaultConfig = true
    allRules = false

    reports {
        html.enabled = true
        xml.enabled = true
        txt.enabled = true
        sarif.enabled = true
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv('OSSRH_USERNAME')
            password = System.getenv('OSSRH_PASSWORD')
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = "Micronaut WireMock Support"
                description = "WireMock integration for Micronaut"
                url = "https://github.com/rapatao/micronaut-wiremock"
                packaging = "jar"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "rapatao"
                        name = "Luiz Henrique Rapatao"
                        email = "rapatao@rapatao.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/rapatao/micronaut-wiremock.git"
                    developerConnection = "scm:git:ssh://github.com/rapatao/micronaut-wiremock.git"
                    url = "https://github.com/rapatao/micronaut-wiremock"
                }
            }
        }
    }
}
